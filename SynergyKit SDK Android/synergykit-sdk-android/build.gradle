apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

//define constants
def sdkVersion = '2.1.7'
def siteUrl = 'https://synergykit.com'      // Homepage URL of the library
def gitUrl = 'https://github.com/Letsgood/synergykit-sdk-android'   // Git repository URL


android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 21
        versionCode 1
        versionName version
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:22+'
    compile 'com.google.code.gson:gson:2.2.4'
    compile ('com.github.nkzawa:socket.io-client:0.3.0'){
        exclude group: 'org.json', module: 'json'
    }
}

//maven attributes
group = "com.letsgood"                                        // Maven Group ID for the artifact
version = sdkVersion

//install maven
install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'

                // Add your description here
                name 'synergykit-sdk-android'
                url siteUrl


                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl

                }
            }


        }
    }
}

// create jar
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

//artifact to run jar creation
artifacts {
    archives sourcesJar
}

//load local properties
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

//bintray upload
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = "maven"
        userOrg = 'letsgood'
        name = "synergykit-sdk-android"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true

        //Optional version descriptor
        version {
            name = sdkVersion //Bintray logical version name
            desc = 'Synergykit Android SDK ' + sdkVersion
            vcsTag = sdkVersion
            // attributes = ['gradle-plugin': 'com.use.less:com.use.less.gradle:gradle-useless-plugin'] //Optional version-level attributes
//            gpg {
//                sign = true //Determines whether to GPG sign the files. The default is false
//                passphrase = 'passphrase' //Optional. The passphrase for GPG signing'
//            }
//            mavenCentralSync {
//                sync = true //Optional (true by default). Determines whether to sync the version to Maven Central.
//                user = 'userToken' //OSS user token
//                password = 'paasword' //OSS user password
//                close = '1' //Optional property. By default the staging repository is closed and artifacts are released to Maven Central. You can optionally turn this behaviour off (by puting 0 as value) and release the version manually.
//            }
        }
    }

}

